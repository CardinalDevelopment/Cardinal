import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

group = 'in.twizmwaz'
version = '1.9-SNAPSHOT'
project.description = 'An open source implementation of PGM'
String mainClass = 'in.twizmwaz.cardinal.Cardinal'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
    maven { url 'https://repo.oc.tc/content/groups/public/' }
    maven { url 'http://ci.twizmwaz.in/nexus/content/repositories/release/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    compile 'org.projectlombok:lombok:1.16.6'
    compile 'tc.oc:sportbukkit:1.8.7-R0.1-SNAPSHOT'
    compile 'ee.ellytr.command:EllyCommand:1.0.0'
    compile 'org.jdom:jdom2:2.0.6'
    compile 'org.ow2.asm:asm-all:5.0.4'

    testCompile('junit:junit:4.10')
}

processResources {
    filter ReplaceTokens, tokens: [
        'name': rootProject.name,
        'description': project.description,
        'mainClass': mainClass,
        'version': version
    ]
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    append('LICENSE')
    dependencies {
        include(dependency('org.jdom:jdom2:2.0.6'))
        relocate 'org.jdom2', 'in.twizmwaz.cardinal.libs.jdom2'
        include(dependency('org.ow2.asm:asm-all:5.0.4'))
        relocate 'org.objectweb.asm', 'in.twizmwaz.cardinal.libs.asm'
        include(dependency('ee.ellytr.command:EllyCommand:1.0.0'))
    }
}

build.dependsOn shadowJar

apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '6.14.1'
    configFile = project.file('checkstyle.xml')
}

apply plugin: 'license'

license {
    include '**/*.java'
    ignoreFailures = true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
